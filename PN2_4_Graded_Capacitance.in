go atlas

# Graded Junction Case
# Adding the meshings
mesh
x.m l=0.0 spac=3.0
x.m l=18.0 spac=3.0
y.m l=0.0 spac=3.0
y.m l=22.5 spac=0.3
y.m l=45.0 spac=1.2

region num=1 silicon

# Naming the P-Type Silicon Anode and N-Type Silicon Cathode
electrode top name=anode
electrode bottom name=cathode

# Graded Doping Concentration
doping gaussian p.type conc=1e19 peak=0.0 junc=22.5 char=2
doping gaussian n.type conc=1e19 peak=45.0 junc=22.5 char=2

# Physical Models
models srh conmob bgn auger fldmob k.p fermi incomplete consrh optr print

# Impact Ionization model
impact selb

# Adding newton numerical model 
method newton

# Solving the P-N junction in equilibrium
solve init

# Getting all the Carrier Concentrations and Energy bands and diagrams
output band.temp jdiffusion jdrift

save outf=pn2_graded_junction.str

# Log file for AC analysis results for positive bias
log outf=pn2_graded_junction_cv_pos.log

# Perform DC and AC sweep from 0V to 5V
solve name=anode vanode=0.0 vstep=0.05 vfinal=5.0 ac freq=1e6 qscv nocurrent

# Extracting and saving the junction capacitance values versus bias voltage (positive bias)
extract name="junction_cap_vs_pos_bias" curve(v."anode", c."anode""cathode") outfile="junction_cap_graded_vs_pos_bias.dat"

# Log file for AC analysis results for negative bias
log outf=pn2_graded_junction_cv_neg.log

# Perform DC and AC sweep from 0V to -5V
solve name=anode vanode=0.0 vstep=-0.05 vfinal=-5.0 ac freq=1e6 qscv nocurrent

# Extracting and saving the junction capacitance values versus bias voltage (negative bias)
extract name="junction_cap_vs_neg_bias" curve(v."anode", c."anode""cathode") outfile="junction_cap_graded_vs_neg_bias.dat"

quit
