# (c) Silvaco Inc., 2019
go athena

# Define the mesh
line x loc=0.0 spac=0.1 
line x loc=0.2 spac=0.006
line x loc=0.4 spac=0.006
line x loc=0.6 spac=0.01 

line y loc=0.0 spac=0.002 
line y loc=0.2 spac=0.005
line y loc=0.5 spac=0.05
line y loc=0.8 spac=0.15 

# Initialize the silicon without any background doping
init orientation=100 space.mul=2

# Define p-type doping
doping p.type conc=1e16

# P-well formation including masking off of the nwell
diffus time=30 temp=1000 dryo2 press=1.00 hcl=3

etch oxide thick=0.02

# P-well Implant
implant boron dose=8e12 energy=100 pears 

diffus temp=950 time=100 weto2 hcl=3

# Well drive
diffus time=50 temp=1000 t.rate=4.000 dryo2 press=0.10 hcl=3
diffus time=220 temp=1200 nitro press=1
diffus time=90 temp=1200 t.rate=-4.444 nitro press=1

etch oxide all

# Sacrificial "cleaning" oxide
diffus time=20 temp=1000 dryo2 press=1 hcl=3
etch oxide all

# Gate oxide grown here with the specified thickness of 500 Ã…
diffus time=30 temp=925 dryo2 press=1.00 hcl=3

# Extract a design parameter 
extract name="gateox" thickness oxide mat.occno=1 x.val=0.05

# Vt adjust implant 
implant boron dose=9.5e11 energy=10 pearson 

# Deposit polysilicon gate with uniform doping
depo poly thick=0.2 divi=10 
doping uniform n.type conc=1e20

# From now on the situation is 2-D
etch poly left p1.x=0.35

method fermi compress
diffuse time=3 temp=900 weto2 press=1.0

implant phosphor dose=3.0e13 energy=20 pearson 

depo oxide thick=0.120 divisions=8
etch oxide dry thick=0.120

implant arsenic dose=5.0e15 energy=50 pearson 

method fermi compress
diffuse time=1 temp=900 nitro press=1.0

# Pattern S/D contact metal
etch oxide left p1.x=0.2
deposit alumin thick=0.03 divi=2
etch alumin right p1.x=0.18

# Extract design parameters
extract name="nxj" xj silicon mat.occno=1 x.val=0.1 junc.occno=1
extract name="n++ sheet rho" sheet.res material="Silicon" mat.occno=1 x.val=0.05 region.occno=1
extract name="ldd sheet rho" sheet.res material="Silicon" mat.occno=1 x.val=0.3 region.occno=1
extract name="chan surf conc" surf.conc impurity="Net Doping" material="Silicon" mat.occno=1 x.val=0.45

structure mirror right

electrode name=gate x=0.5 y=0.1
electrode name=source x=0.1
electrode name=drain x=1.1
electrode name=substrate backside

structure outfile=mos1ex01_0.str

# Plot the structure
tonyplot mos1ex01_0.str

############# Vt Test : Returns Vt, Beta and Theta ################
go atlas

# Set material models
models cvt srh conmob bgn auger fldmob k.p fermi incomplete consrh optr print

# Impact Ionization model
impact selb

# Getting all the Carrier Concentrations and Energy bands and diagrams
output band.temp jdiffusion jdrift

contact name=gate n.poly
interface qf=3e10

method newton
solve init

# Apply gate bias 
solve vdrain=0.1 vgate=2.5 name=gate
log outf=mos1ex01_1.log master
save outf=mos1ex01_1.str

# Plot results
tonyplot mos1ex01_1.log

# Extract device parameters
extract name="nvt" (xintercept(maxslope(curve(abs(v."gate"),abs(i."drain")))) - abs(ave(v."drain"))/2.0)
extract name="nbeta" slope(maxslope(curve(abs(v."gate"),abs(i."drain")))) * (1.0/abs(ave(v."drain")))
extract name="ntheta" ((max(abs(v."drain")) * $"nbeta")/max(abs(i."drain"))) - (1.0 / (max(abs(v."gate")) - ($"nvt")))

quit
